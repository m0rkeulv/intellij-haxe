/*
 * Copyright 2000-2013 JetBrains s.r.o.
 * Copyright 2014-2018 AS3Boyan
 * Copyright 2014-2014 Elias Ku
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Source generation tested and working with Haxe Compiler 3.4.7
 */

runIde.enabled = false
patchPluginXml.enabled = false
verifyPlugin.enabled = false


def findHaxeVersion() {
    def so = new ByteArrayOutputStream()
    def eo = new ByteArrayOutputStream()
    try {
        exec {
            commandLine 'haxe', '-version'
            standardOutput = so
            errorOutput = eo
        }
        //haxe returns version (3.4.7) in error output for some reason
        ext.haxeVersion = eo.toString()
    }
    catch (Exception e) {
        ext.haxeVersion = null

    } finally {
        so.close()
        eo.close()
    }
    if (ext.haxeVersion != null)  {
        println("Haxe found: $ext.haxeVersion")
        println("Gradle will try to generate hxcpp debugger protocol if nessesary")
        ext.hxcppGeneratedFolder = "src/generated/java"
    }else {
        println("Haxe not found ")
        println("Gradle will use provided hxcpp debugger protocol source")
        ext.hxcppGeneratedFolder = "src/fallback/java"
    }
}
findHaxeVersion()


ext.hxcppSrcFolder = "src/main/haxe"
sourceSets.main.java.srcDir "$hxcppGeneratedFolder/src"

task installHxJava(group: "hxcpp") {
    doLast {
        exec {
            commandLine 'haxelib', 'install', 'hxjava'
        }
    }
}
task installHxcppDebugger(group: "hxcpp") {
    doLast {
        exec {
            commandLine 'haxelib', 'git', 'hxcpp-debugger', 'https://github.com/HaxeFoundation/hxcpp-debugger.git', 'protocol_v1.1'
        }
    }

}

task generateDebuggerJavaSource(type: Exec, group: "hxcpp") {
    dependsOn("installHxJava")
    dependsOn("installHxcppDebugger")

    inputs.file "${hxcppSrcFolder}/JavaProtocol.hx"
    outputs.dir "${hxcppGeneratedFolder}/src"

    workingDir 'src/main/haxe/'
    commandLine 'haxe', '-cp', '..', '-java', '../../generated/java/', '-main', 'JavaProtocol', '-lib', 'hxcpp-debugger'
}

clean.dependsOn('cleanGenerated')
task cleanGenerated(type: Delete, group: 'generate') {
    delete "$hxcppGeneratedFolder"
}

cleanGenerated.onlyIf { haxeVersion != null }
generateDebuggerJavaSource.onlyIf { haxeVersion != null }
installHxcppDebugger.onlyIf { haxeVersion != null }
installHxJava.onlyIf { haxeVersion != null }

compileJava.dependsOn('generateDebuggerJavaSource')